ext {
    nav_version_ktx = '2.3.0'
    material_version = '1.3.0-alpha02'
    lifecycle_version = '2.3.0-alpha06'
    room_version = '2.2.5'
    work_version = '2.4.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    def buildDateTime = projects.hasProperty("debug") ? 0 : new Date().format('yyMMddHHmm').toInteger()

    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId 'io.ikws4.weiju'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode buildDateTime
        versionName "0.3.6"
    }
    buildTypes {
        release {
            minifyEnabled true
            //applicationIdSuffix ".release"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            //debuggable true
            //applicationIdSuffix ".debug"
            //signingConfig signingConfigs.debug
            ext.alwaysUpdateBuildId = false
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha01'
    implementation 'androidx.core:core-ktx:1.5.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta8'
    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version_ktx"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version_ktx"
    // preference
    implementation 'androidx.preference:preference-ktx:1.1.1'
    // lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    // safe room
    implementation "com.commonsware.cwac:saferoom.x:1.1.1"
    // workManager
    implementation "androidx.work:work-runtime-ktx:$work_version"
    // material
    implementation "com.google.android.material:material:$material_version"

    implementation 'com.google.code.gson:gson:2.8.6'

    // material-dialogs
    implementation 'com.afollestad.material-dialogs:core:3.1.0'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.1.0'
    implementation 'com.afollestad.material-dialogs:input:3.1.0'
    //implementation 'com.afollestad.material-dialogs:color:3.1.0'

    // okHttp
    implementation 'com.squareup.okhttp3:okhttp:4.7.2'

    // markdown view
    //implementation 'com.github.tiagohm.MarkdownView:library:0.19.0'

    // xposed
    compileOnly 'de.robv.android.xposed:api:82'
    implementation 'com.github.ikws4:xposed-ktx:1.0'

    //implementation project(':flutter')
}

task updateVirtualXposedAPP(type: Exec) {
    def pkg = "io.ikws4.weiju"
    commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'update', '-e', 'pkg', pkg
}

task rebootVirtualXposedAPP(type: Exec) {
    commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'reboot'
}

task launchVirtualXposedAPP(type: Exec) {
    def pkg = "io.ikws4.weiju"
    commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'launch', '-e', 'pkg', pkg
}
